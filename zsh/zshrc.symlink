#!/usr/bin/env zsh

if type brew &>/dev/null ; then
  BREW_PREFIX=$(brew --prefix)
else
  BREW_PREFIX=/usr/local
fi

###############################################################################
########## Initial 
###############################################################################

# Should be similar to default path binding behavior, but
# this flag binds the PATH scalar environment variable to 
# the lowercase `path` associative array so it can
# be used with zsh path modifiers
#
#     path=(/new/path $path)
#     path+=/appended/path
#
# The x flag will automatically export the variable.
# The U flag makes it remove duplicates keeping only the first value
# of the array.
typeset -TUx PATH path
typeset -TUx MANPATH manpath
typeset -TUx FPATH fpath

# Export default path
path=(/usr/local/bin /usr/local/sbin /usr/bin /usr/sbin /bin /sbin /opt/X11/bin)
manpath=(/usr/local/share/man /usr/share/man /opt/X11/share/man)

# Override system utils with gnu versions (e.g. grep, ls, cp, etc)
path=(/usr/local/opt/coreutils/libexec/gnubin $path)
path=(${HOME}/bin ${ZSH}/bin $path)

###############################################################################
########## Plugin Setup
###############################################################################

autoload -U add-zsh-hook

###############################################################################
########## Custom Plugin Setup
###############################################################################

# Load ZSH plugins
for f in $ZSH/zsh/{plugins,config}/*.zsh ; do source $f ; done

###############################################################################
##########  FPATH Setup
###############################################################################

# fpath is how we configure additional zsh functions (including completion functions).
# It is like a bin folder added to path, but fpath can add zsh executables that are
# function definitions.

typeset -aU completion_dirs
completion_dirs=(
  ${BREW_PREFIX}/share/zsh-completions
  ${BREW_PREFIX}/share/zsh/site-functions
  ${ZSH}/zsh/functions
  ${ZSH}/zsh/completion
)
for d in $completion_dirs ; do
  if [[ -d $d ]] ; then
    fpath=($d $fpath)
    # Autoload each executable inside the directory
    #   -U supresses alias expansion while autoloading
    #   -z uses ZSH style
    #   (:t) trims the tail (file extension) from the glob qualifier.
    autoload -Uz $d/*(:t)
  fi
done
unset completion_dirs

autoload -Uz compinit; compinit

###############################################################################
##########  Prompt Configuration
###############################################################################

# Don't load pure prompt if it is not installed
if type prompt_pure_setup &>/dev/null ; then
  autoload -Uz promptinit; promptinit
  # Configure prompt here
  # https://github.com/sindresorhus/pure#options

  # PURE_CMD_MAX_EXEC_TIME=10
  # zstyle :prompt:pure:git:stash show yes
  prompt pure
fi

#########################################


# localrc will override any path/completion files specified in the dotfiles repo
# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -s ~/.localrc ]] ; then
  source ~/.localrc
fi
