# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=$HOME/dev

export SYS_ARCH=$(uname -a | cut -d' ' -f1)
if [ "$SYS_ARCH" = Darwin ]
then
    export IS_OSX=1
    export MAJOR_OSX_VERS=$(sw_vers -productVersion | awk -F "." '{print $1 "." $2}')
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# reset PATH so reloading does not just append
export PATH="/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin"

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
